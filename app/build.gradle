apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: 'signing.gradle'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.vkr.vkrmobile"
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 100000
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    apply plugin: 'kotlin-kapt'

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                def versionCode = variant.versionCode
                def flavorName = variant.productFlavors[0].properties['applicationId'].toString().replaceAll('\\.', '_')
                outputFileName = "[$flavorName][v$versionCode].apk"
            }
        }

        variant.assemble.doLast { assemble ->
            copy {
                from variant.outputs*.outputFile
                into new File(rootProject.projectDir, "apk/")
            }
        }
    }

    flavorDimensions "default"

    signingConfigs.whenObjectAdded { signing ->
        setupSigning(signing.name)
    }

    productFlavors.whenObjectAdded { flavor ->
        flavor.signingConfig = android.signingConfigs[flavor.name]
    }

    apply from: 'flavors.gradle'
}

kapt {
    arguments {
        arg("toothpick_registry_package_name", "com.vkr.vkrmobile")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

//    RX
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"
    implementation "io.reactivex.rxjava3:rxjava:3.0.4"
    implementation "io.reactivex.rxjava3:rxkotlin:3.0.0"

//    Toothpick
    implementation 'com.github.stephanenicolas.toothpick:ktp:3.0.2'
    kapt 'com.github.stephanenicolas.toothpick:toothpick-compiler:3.0.2'

//    Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

//    Moxy
    implementation "com.github.moxy-community:moxy:2.1.2"
    kapt "com.github.moxy-community:moxy-compiler:2.1.2"
    implementation "com.github.moxy-community:moxy-androidx:2.1.2"
    implementation "com.github.moxy-community:moxy-material:2.1.2"
    implementation "com.github.moxy-community:moxy-ktx:2.1.2"

//    Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.9.0"
    implementation "com.github.akarnokd:rxjava3-retrofit-adapter:3.0.0"

//    Cicerone
    implementation 'ru.terrakok.cicerone:cicerone:5.1.1'

//    Google Material
    implementation 'com.google.android.material:material:1.1.0'
}